<!DOCTYPE html>
<html style="width:100%;height:100%;margin:0px;padding:0px;overflow:hidden">
<script type="text/javascript" src="./three/three.js"></script>
<script type="text/javascript" src="./three/OrbitControls.js"></script>
<script type="text/javascript" src="./three/stats.min.js"></script>
<script type="text/javascript" src="./simulate/shaderTexture.js"></script>
<script tyep="text/javascript" src="./simulate/pingpong.js"></script>
<script type="text/javascript" src="./simulate/simulator.js"></script>
<script tyep="text/javascript" src="./textures/NodeProcessor.js"></script>
<script type="text/javascript" src="./main.js"></script>

<!-- pass through vertex shader -->
<script id="passThroughVertex" type="x-shader/x-vertex">
    void main()	{
        gl_Position = vec4( position, 1.0 );
    }
</script>

<!-- pass through fragment shader -->
<script id="passThroughFragment" type="x-shader/x-fragment">
    uniform vec2 resolution;
    //input texture from the previous run of this shader on the ping / pong
    uniform sampler2D texture;
    void main()	{
        vec2 uv = gl_FragCoord.xy / resolution.xy;
        vec3 color = texture2D( texture, uv ).xyz;
        gl_FragColor = vec4( color, 1.0 );
    }
</script>

<script id="nodeFragment" type="x-shader/x-fragment">
    uniform vec2 resolution;
    uniform float delta;
    uniform float textureWidth;

    uniform sampler2D positions;
    uniform sampler2D edgeIndices;
    uniform sampler2D edges;

    //we set this text in javascript based on the size of the textureWidth
    const float mainTexDelta = 1.0 /;

    //sete this text in javascript as well based on the size of the edge texture width(may be smaller)
    const float edgeTexDelta = 1.0 /;

    vec3 samplePositionTexture(float textureIndex, vec4 curr){
        vec4 neighbor =  texture2D(positions, vec2(((mod(textureIndex,textureWidth)) / textureWidth),(floor(textureIndex / textureWidth)) / textureWidth));
        float distanceToNeighbor = distance(curr,neighbor);
        return vec3(neighbor.x - curr.x, neighbor.y - curr.y, neighbor.z - curr.z);
    }

    void main()	{

        //get the position of this node in the "main" textures
        vec2 mainTextureCoord = gl_FragCoord.xy / resolution.xy;

        //get the positions of this node (in space) from the texture
        vec4 currentNodePosition = texture2D(positions, mainTextureCoord);

        //the deltas for this node
        vec4 nodeDiff = vec4(0.0,0.0,0.0,0.0);

        if(!(currentNodePosition.x == -1.0 && currentNodePosition.y == -1.0 && currentNodePosition.z == -1.0 && currentNodePosition.w == -1.0)){

            //compare against every other node
            for(float currentX = 0.0; currentX < 1.0; currentX += mainTexDelta){
                for(float currentZ= 0.0; currentZ< 1.0; currentZ+= mainTexDelta){
                    //continue if we're evaluating against the current node
                    if(currentX == mainTextureCoord.x){
                        if(currentZ== mainTextureCoord.y){
                            continue;
                        }
                    }

                    //get the node we're comparing againsts position
                    vec2 currentCoord = vec2(currentX,currentZ);
                    vec4 compareNodePosition = texture2D(positions,currentCoord);
                    //this is an empty / unused node in the texture
                    if(compareNodePosition.w == -1.0){
                        continue;
                    }

                    //if they're close enough together, compute forces
                    float nodeDistance = distance(compareNodePosition,currentNodePosition);
                    if(nodeDistance < 20.0){
                        //get the difference between the nodes
                        nodeDiff += currentNodePosition - compareNodePosition;
                    }
                }
            }

            //get the indices of the nodes connected to this node in the "edges" texture
            vec4 currentNodeEdgeIndices = texture2D(edgeIndices, mainTextureCoord);
            float beginCell = currentNodeEdgeIndices.x;
            float endCell = currentNodeEdgeIndices.z;
            float beginComponent = currentNodeEdgeIndices.y;
            float endComponent = currentNodeEdgeIndices.w;


            if(!(beginCell == endCell && beginComponent == endComponent)){

                //keep track of which edge we're on
                float edgeIndex = 0.0;
                //loop through the edge tex
                for(float currentY  = 0.0; currentY < 1.0; currentY += edgeTexDelta){
                    for(float currentX = 0.0; currentX < 1.0; currentX += edgeTexDelta){

                        //we're in bounds, check individual tex coords
                        vec4 possibleNeighbors = texture2D(edges,vec2(currentX,currentY));

                        //check to see if we're in the right cell
                        if(edgeIndex >= beginCell && edgeIndex <= endCell){

                            //check x component
                            if((edgeIndex > beginCell && endComponent > 0.0) ||
                                (edgeIndex == beginCell && beginComponent <= 0.0 && (endComponent > 0.0  || endCell > beginCell))){
                                vec3 neighborDeltas = samplePositionTexture(possibleNeighbors.x, currentNodePosition);
                                nodeDiff.x += neighborDeltas.x;
                                nodeDiff.y += neighborDeltas.y;
                                nodeDiff.z += neighborDeltas.z;
                            }

                            //check y component
                            if((edgeIndex > beginCell && endComponent > 1.0) ||
                                (edgeIndex == beginCell && beginComponent <= 1.0 && (endComponent > 1.0  || endCell > beginCell))){
                                vec3 neighborDeltas = samplePositionTexture(possibleNeighbors.y, currentNodePosition);
                                nodeDiff.x += neighborDeltas.x;
                                nodeDiff.y += neighborDeltas.y;
                                nodeDiff.z += neighborDeltas.z;
                            }

                            //check z component
                            if((edgeIndex > beginCell && endComponent > 2.0) ||
                                (edgeIndex == beginCell && beginComponent <= 2.0 && (endComponent > 2.0  || endCell > beginCell))){
                                vec3 neighborDeltas = samplePositionTexture(possibleNeighbors.z, currentNodePosition);
                                nodeDiff.x += neighborDeltas.x;
                                nodeDiff.y += neighborDeltas.y;
                                nodeDiff.z += neighborDeltas.z;
                            }

                            //check w component
                            if((edgeIndex > beginCell && endComponent > 3.0) ||
                                (edgeIndex == beginCell && beginComponent <= 3.0 && (endComponent > 3.0 || endCell > beginCell))){
                                vec3 neighborDeltas = samplePositionTexture(possibleNeighbors.w, currentNodePosition);
                                nodeDiff.x += neighborDeltas.x;
                                nodeDiff.y += neighborDeltas.y;
                                nodeDiff.z += neighborDeltas.z;
                            }

                        }

                        //increment the edge so we know where we are
                        edgeIndex += 1.0;
                    }//edge tex loop
                }//edge tex loop
            }//if there are edges going to this node

        }//if this isn't an empty node

        if(!(nodeDiff.x == 0.0 && nodeDiff.y == 0.0 && nodeDiff.z == 0.0)){
            nodeDiff = normalize(nodeDiff);
            gl_FragColor = vec4(currentNodePosition.x + nodeDiff.x * 0.1, currentNodePosition.y + nodeDiff.y * 0.1, currentNodePosition.z + nodeDiff.z * 0.1, 0 );
        }
        else{
            gl_FragColor = vec4(currentNodePosition.x, currentNodePosition.y , currentNodePosition.z, 0 );
        }



    }
</script>

<script type="x-shader/x-vertex" id="cloudVertex">
    attribute vec3 customColor;
    attribute float size;
    attribute vec2 texPos;

    uniform sampler2D positionTexture;

    varying vec3 vColor;

    void main() {

        vec4 NodePosition = texture2D(positionTexture,texPos);

        vColor = customColor;
        vec4 mvPosition = modelViewMatrix * vec4( NodePosition.x,NodePosition.y,NodePosition.z, 1.0 );
        gl_PointSize = size * ( 300.0 / length( mvPosition.xyz ) );

        gl_Position = projectionMatrix * mvPosition;
    }
</script>

<script type="x-shader/x-fragment" id="cloudFragment">

    varying vec3 vColor;

    void main() {
        gl_FragColor = vec4(vColor,1.0);
    }

</script>

<script type="x-shader/x-vertex" id="edgeVertex">
    attribute vec2 texPos;

    uniform sampler2D positionTexture;
    uniform vec3 color;

    varying vec3 vColor;

    void main() {

        vec4 NodePosition = texture2D(positionTexture,texPos);

        vColor = color;
        vec4 mvPosition = modelViewMatrix * vec4( NodePosition.x,NodePosition.y,NodePosition.z, 1.0 );

        gl_Position = projectionMatrix * mvPosition;
    }
</script>

<script type="x-shader/x-fragment" id="edgeFragment">

    varying vec3 vColor;

    void main() {
        gl_FragColor = vec4(vColor,1.0);
    }

</script>

<head lang="en">
    <meta charset="UTF-8">
    <title></title>
</head>
<body style="width:100%;height:100%;margin:0px;padding:0px;overflow:hidden" onload="initialize()">
</body>
</html>